
buildscript {
  repositories {
    mavenLocal()
    gradlePluginPortal()
    maven { url 'https://dl.bintray.com/brambolt/public' }
    if (project.hasProperty('mavenContextUrl'))
      maven {
        url "${mavenContextUrl}/${mavenRepoKey}"
        credentials {
          username(mavenUser as String)
          password(mavenToken as String)
        }
      }
    jcenter()
    mavenCentral()
  }
  dependencies {
    classpath "com.brambolt.wrench:brambolt-wrench-properties:${bramboltVersion}"
    classpath "com.jfrog.artifactory:com.jfrog.artifactory.gradle.plugin:${artifactoryVersion}"
  }
}

plugins {
  id 'groovy'
  id 'java-library'
  id 'com.brambolt.gradle.velocity' apply false
  id 'maven-publish'
}

description = 'Sample properties.'
group = 'com.brambolt.wrench'
version = bramboltVersion

ext {
  artifactId = 'brambolt-wrench-properties-sample'
}

apply plugin: 'com.brambolt.gradle.velocity'

dependencies {
  implementation "com.brambolt.wrench:brambolt-wrench-properties:${version}"
  testImplementation "com.brambolt.gradle:brambolt-gradle-testkit:${version}"
}
sourceSets {
  main {
    resources {
      srcDirs "${project.buildDir}/templates"
    }
  }
}

velocity {
  strict = true
  context(
    bramboltVersion: project.bramboltVersion,
    buildNumber: project.buildNumber,
    version: project.version
  )
  outputDir = new File(project.buildDir, 'templates')
}

processResources.dependsOn(velocity)

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project.group
      artifactId project.artifactId
      version project.version
      from components.java
    }
  }
}

apply plugin: 'com.jfrog.artifactory'

artifactory {
  contextUrl = project.artifactoryContextUrl
  publish {
    repository {
      repoKey = project.artifactoryRepoKey
      username = project.artifactoryUser
      password = project.artifactoryToken
      maven = true
    }
    defaults {
      publications('mavenJava')
      publishArtifacts = true
      publishPom = true
    }
  }
  resolve {
    repository {
      repoKey = project.artifactoryRepoKey
      username = project.artifactoryUser
      password = project.artifactoryToken
      maven = true
    }
  }
}


task local(dependsOn: publishToMavenLocal)

task all(dependsOn: local)

all.dependsOn(artifactoryPublish)
